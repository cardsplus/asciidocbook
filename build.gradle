/*
 * https://guides.gradle.org/creating-new-gradle-builds/
 * https://github.com/asciidoctor/asciidoctor-gradle-plugin
 */

plugins {
    id 'java'
    id "com.palantir.git-version" version "0.12.3"
    id "org.asciidoctor.jvm.convert" version "3.2.0"
    id 'org.asciidoctor.jvm.pdf' version '3.2.0'
    id 'org.asciidoctor.jvm.gems' version '3.2.0'
}

description = 'cards+ asciidoc book'
version = gitVersion();

repositories {
    jcenter()
    ruby.gems()
}

dependencies {
  asciidoctorGems 'rubygems:rouge:3.22.0'
}

asciidoctorj {
  logLevel 'INFO'
  requires 'rouge'
  modules {
    diagram { 
      version '2.0.2'
    }
    pdf { 
      version '1.5.3'
    }
  }
  options doctype: 'book'
  attributes 'source-highlighter': 'rouge'
  attributes 'icons': 'font'
  attributes 'sectlink': true
  attributes 'sectanchors': true
  attributes 'numbered': true
  attributes 'idprefix': ''
  attributes 'idseparator': '-'
  attributes 'title': description
  attributes 'author': 'Robert Bruckbauer'
  attributes 'email':  'bruckbauer@gmx.at'
  attributes 'website': 'https://cardsplus.info'
  attributes 'revnumber': version
  attributes 'revdate': new Date().format('yyyy/MM/dd')
  attributes 'revyear': new Date().format('yyyy')
  attributes 'imagesdir': 'images'
}

asciidoctor {
  build.dependsOn it
  dependsOn asciidoctorGemsPrepare
  attributes 'toc': 'left'
  attributes 'toclevels': '5'
  languages 'de'
  sourceDir file('doc')
  sources {
    include 'index.adoc'
  }
  outputDir file('build/html')
  outputs.upToDateWhen { false }
}

pdfThemes.local 'a4', { themeDir = 'etc' }
asciidoctorPdf {
  build.dependsOn it
  dependsOn asciidoctorGemsPrepare
  attributes 'toc': 'left'
  attributes 'toclevels': '3'
  languages 'de'
  theme 'a4'
  fontsDir file('fonts')
  sourceDir file('doc')
  sources {
    include 'index.adoc'
  }
  outputDir file('build/pdf')
  outputs.upToDateWhen { false }
}
