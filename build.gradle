/*
 * https://guides.gradle.org/creating-new-gradle-builds/
 * https://github.com/asciidoctor/asciidoctor-gradle-plugin
 */

plugins {
    id 'base'
    id "com.palantir.git-version" version "0.12.3"
    id "org.asciidoctor.jvm.convert" version "3.3.0"
    id 'org.asciidoctor.jvm.pdf' version '3.3.0'
    id 'org.asciidoctor.jvm.gems' version '3.3.0'
}

description = 'cards+ asciidoc book'
version = gitVersion();

repositories {
    jcenter()
    ruby.gems()
}

dependencies {
  asciidoctorGems 'rubygems:rouge:3.22.0'
}

// tag::adoc[]
asciidoctorj {
  requires 'rouge'
  requires file('src/main/ruby/glob-include-processor.rb')
  modules {
    diagram { 
      version '2.1.0'
    }
    pdf { 
      version '1.5.4'
    }
  }
  logLevel 'INFO'
  options doctype: 'book'
  attributes 'source-highlighter': 'rouge'
  attributes 'sectlink': true
  attributes 'sectanchors': true
  attributes 'numbered': true
  attributes 'idprefix': ''
  attributes 'idseparator': '-'
  attributes 'author': 'Robert Bruckbauer'
  attributes 'email':  'bruckbauer@gmx.at'
  attributes 'website': 'https://cardsplus.info'
  attributes 'revnumber': version
  attributes 'revdate': new Date().format('yyyy/MM/dd')
  attributes 'revyear': new Date().format('yyyy')
  attributes 'revremark': description
}
// end::adoc[]

// tag::html[]
asciidoctor {
  build.dependsOn it
  dependsOn asciidoctorGemsPrepare
  baseDirFollowsSourceDir()
  attributes 'toc': 'left'
  attributes 'toclevels': '5'  
  attributes 'imagesdir': 'images'
  sourceDir file('doc')
  sources {
    include 'index.adoc', 'pitch.adoc'
  }
  resources {
    from('doc') {
      include '**/*.jpg'
      include '**/*.png'
      include '**/*.svg'
    }
    into './images'
  }
  outputDir file('pages/html')
  outputs.upToDateWhen { false }
}
task html(dependsOn: 'asciidoctor')
// end::html[]

// tag::pdf[]
pdfThemes.local 'a4', { themeDir = 'etc' }
asciidoctorPdf {
  build.dependsOn it
  dependsOn asciidoctorGemsPrepare
  baseDirFollowsSourceDir()
  attributes 'toc': 'left'
  attributes 'toclevels': '3'
  attributes 'imagesdir': file('doc')
  attributes 'imagesoutdir': file('build/pdf/images')
  theme 'a4'
  fontsDir file('fonts')
  sourceDir file('doc')
  sources {
    include 'index.adoc'
  }
  outputDir file('pages/pdf')
  outputs.upToDateWhen { false }
}
task pdf(dependsOn: 'asciidoctorPdf')
// end::pdf[]
