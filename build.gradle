/*
 * https://guides.gradle.org/creating-new-gradle-builds/
 * https://github.com/asciidoctor/asciidoctor-gradle-plugin
 */

plugins {
    id 'base'
    id "com.palantir.git-version" version "0.12.3"
    id "org.asciidoctor.jvm.convert" version "3.2.0"
    id 'org.asciidoctor.jvm.pdf' version '3.2.0'
    id 'org.asciidoctor.jvm.gems' version '3.2.0'
}

description = 'cards+ asciidoc book'
version = gitVersion();

repositories {
    jcenter()
    ruby.gems()
}

dependencies {
  asciidoctorGems 'rubygems:rouge:3.22.0'
}

asciidoctorj {
  logLevel 'INFO'
  requires 'rouge'
  requires file('src/main/ruby/glob-include-processor.rb')
  modules {
    diagram { 
      version '2.0.2'
    }
    pdf { 
      version '1.5.3'
    }
  }
  options doctype: 'book'
  attributes 'source-highlighter': 'rouge'
  attributes 'sectlink': true
  attributes 'sectanchors': true
  attributes 'numbered': true
  attributes 'idprefix': ''
  attributes 'idseparator': '-'
  attributes 'author': 'Robert Bruckbauer'
  attributes 'email':  'bruckbauer@gmx.at'
  attributes 'website': 'https://cardsplus.info'
  attributes 'revnumber': version
  attributes 'revdate': new Date().format('yyyy/MM/dd')
  attributes 'revyear': new Date().format('yyyy')
  attributes 'revremark': description
}

asciidoctor {
  build.dependsOn it
  dependsOn asciidoctorGemsPrepare
  languages 'de'
  baseDirFollowsSourceDir()
  attributes 'toc': 'left'
  attributes 'toclevels': '5'
  attributes 'imagesdir': 'images'
  sourceDir file('doc')
  sources {
    include 'index.adoc'
  }
  resources {
    from('doc') {
      include '**/*.png'
    }
    into './images'
  }
  outputDir file('build/html')
  outputs.upToDateWhen { false }
}

pdfThemes.local 'a4', { themeDir = 'etc' }
asciidoctorPdf {
  build.dependsOn it
  dependsOn asciidoctorGemsPrepare
  languages 'de'
  baseDirFollowsSourceDir()
  attributes 'toc': 'left'
  attributes 'toclevels': '3'
  attributes 'imagesdir': file('doc')
  theme 'a4'
  fontsDir file('fonts')
  sourceDir file('doc')
  sources {
    include 'index.adoc'
  }
  outputDir file('build/pdf')
  outputs.upToDateWhen { false }
}

task html(dependsOn: 'asciidoctor')
task pdf(dependsOn: 'asciidoctorPdf')
